#-------------------------------------------------------------------------
# TITLE: 
#    element_package.tcl
#
# AUTHOR:
#    Will Duquette
# 
# PROJECT:
#    Quill: Project build system for Tcl/TK
#
# DESCRIPTION:
#    Project Element: package skeleton
#
#-------------------------------------------------------------------------

#-------------------------------------------------------------------------
# package Templates

::quillapp::element public package {package} 1 1 \
     ::quillapp::packageElement

# package package
#
# package - The package name
# 
# Saves the package element tree.

proc ::quillapp::packageElement {package} {
    gentree \
        lib/$package/pkgIndex.tcl    [pkgIndex $package]    \
        lib/$package/pkgModules.tcl  [pkgModules $package]  \
        lib/$package/$package.tcl    [pkgFile $package]     \
        test/$package/all_tests.test [allTests $package]    \
        test/$package/$package.test  [testFile $package]
}

# pkgIndex package
#
# pkgIndex.tcl file for the $package(n) package.

maptemplate ::quillapp::pkgIndex {package} {
    set project     [project name]
    set description [project description]
    set version     [project version]
} {
    #-------------------------------------------------------------------------
    # TITLE: 
    #    pkgIndex.tcl
    #
    # PROJECT:
    #    %project: %description
    #
    # DESCRIPTION:
    #    %package(n): pkgIndex file
    #
    #    Generated by Quill
    #
    #-------------------------------------------------------------------------

    # -quill-begin-ifneeded DO NOT EDIT BY HAND
    package ifneeded %package %version [list source [file join $dir pkgModules.tcl]]
    # -quill-end-ifneeded
}

# pkgModules package
#
# pkgModules.tcl file for the $package(n) package.

maptemplate ::quillapp::pkgModules {package} {
    set project     [project name]
    set description [project description]
    set version     [project version]
} {
    #-------------------------------------------------------------------------
    # TITLE: 
    #    pkgModules.tcl
    #
    # PROJECT:
    #    %project: %description
    #
    # DESCRIPTION:
    #    %package(n): Package Loader
    #
    #    Generated by Quill
    #
    #-----------------------------------------------------------------------

    # -quill-begin-provide DO NOT EDIT BY HAND
    package provide %package %version
    # -quill-end-provide

    #-----------------------------------------------------------------------
    # Get the library directory

    namespace eval ::%package:: {
        variable library [file dirname [info script]]
    }

    source [file join $::%package::library %package.tcl]
}

# pkgFile package
#
# $package.tcl file for the $package(n) package.

maptemplate ::quillapp::pkgFile {package} {
    set project     [project name]
    set description [project description]
} {
    #-------------------------------------------------------------------------
    # TITLE: 
    #    %package.tcl
    #
    # PROJECT:
    #    %project: %description
    #
    # DESCRIPTION:
    #    %package(n): Implementation File
    #
    #-------------------------------------------------------------------------

    #-------------------------------------------------------------------------
    # Exported Commands

    namespace eval ::%package {
        namespace export \\
            hello
    }

    #-------------------------------------------------------------------------
    # Commands

    # hello args
    #
    # Dummy procedure

    proc ::%package::hello {arglist} {
        puts "Hello, world!"
        puts "Args: <$arglist>"
    }
}

# allTests package
#
# all_tests.test file for the $package(n) package.

maptemplate ::quillapp::allTests {package} {
    set project     [project name]
    set description [project description]
} {
    #-------------------------------------------------------------------------
    # TITLE:
    #    all_tests.test
    #
    # PROJECT:
    #    %project: %description
    #
    # DESCRIPTION:
    #    %package(n): Test Suite
    #-------------------------------------------------------------------------

    #-------------------------------------------------------------------------
    # Load the tcltest package

    if {[lsearch [namespace children] ::tcltest] == -1} {
        package require tcltest 2.3
        eval ::tcltest::configure $argv
    }

    ::tcltest::configure \
        -testdir [file dirname [file normalize [info script]]] \
        -notfile all_tests.test

    ::tcltest::runAllTests
}

# testFile package
#
# $package.test file for the $package(n) package.

maptemplate ::quillapp::testFile {package} {
    set project     [project name]
    set description [project description]
} {
    #-------------------------------------------------------------------------
    # TITLE:
    #    %package.test
    #
    # PROJECT:
    #    %project: %description
    #
    # DESCRIPTION:
    #    %package(n): Test Suite
    #-------------------------------------------------------------------------

    #-------------------------------------------------------------------------
    # Load the tcltest package

    if {[lsearch [namespace children] ::tcltest] == -1} {
        package require tcltest 2.3
        eval ::tcltest::configure $argv
    }

    namespace import ::tcltest::test

    #-------------------------------------------------------------------------
    # Load the package to be tested

    package require %package
    namespace import ::%package::*

    #-------------------------------------------------------------------------
    # Setup

    # TBD

    #-----------------------------------------------------------------------
    # dummy

    test dummy-1.1 {dummy test} -body {
        set a false
    } -result {true}
}
