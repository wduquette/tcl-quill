#-------------------------------------------------------------------------
# TITLE: 
#    gentree.tcl
#
# AUTHOR:
#    Will Duquette
# 
# PROJECT:
#    Quill: Project build system for Tcl/TK
#
# DESCRIPTION:
#    Project tree generation tools
#
#-------------------------------------------------------------------------

#-------------------------------------------------------------------------
# Exported Commands

namespace eval ::quillapp {
    namespace export \
        gentree
}

#-------------------------------------------------------------------------
# Commands

# gentree path content...
#
# path    - A path relative to the project root, using "/" as the
#           separator.
# content - Content to write to the path.
#
# The input is a set of path and content pairs.

proc ::quillapp::gentree {args} {
    # NEXT, process the files
    foreach {path content} $args {
        # FIRST get the paths
        set p [project root {*}[split $path /]]

        # NEXT, generate the files.
        writefile $p $content
    }
}


#-------------------------------------------------------------------------
# Quillinfo Templates

# elementQuillinfo meta
#
# meta - project metadata dictionary
# 
# Saves the quillinfo element tree.

proc ::quillapp::elementQuillinfo {meta} {
    gentree \
        lib/quillinfo/pkgIndex.tcl   [quillinfoPkgIndex]        \
        lib/quillinfo/pkgModules.tcl [quillinfoPkgModules]      \
        lib/quillinfo/quillinfo.tcl  [quillinfoQuillinfo $meta]
}

# quillinfoPkgIndex
#
# pkgIndex.tcl file for the quillinfo(n) package.

::quill::maptemplate ::quillapp::quillinfoPkgIndex {} {
    set project [project name]
    set description [project description]
} {
    #-------------------------------------------------------------------------
    # TITLE: 
    #    pkgIndex.tcl
    #
    # PROJECT:
    #    %project: %description
    #
    # DESCRIPTION:
    #    %project quillinfo(n): pkgIndex file
    #
    #    Generated by Quill
    #
    #-------------------------------------------------------------------------

    package ifneeded quillinfo 1.0 [list source [file join $dir pkgModules.tcl]]
}

# quillinfoPkgModules
#
# pkgModules.tcl file for the quillinfo(n) package.

::quill::maptemplate ::quillapp::quillinfoPkgModules {} {
    set project [project name]
    set description [project description]

} {
    #-------------------------------------------------------------------------
    # TITLE: 
    #    pkgModules.tcl
    #
    # PROJECT:
    #    %project: %description
    #
    # DESCRIPTION:
    #    %project: quillinfo(n) Package Loader
    #
    #    Generated by Quill
    #
    #-----------------------------------------------------------------------

    package provide quillinfo 1.0

    #-----------------------------------------------------------------------
    # Get the library directory

    namespace eval ::quillinfo:: {
        variable library [file dirname [info script]]
    }

    source [file join $::quillinfo::library quillinfo.tcl]
}

# quillinfoQuillinfo meta
#
# meta - The project metadata dictionary
#
# quillinfo.tcl file for the quillinfo(n) package.

::quill::maptemplate ::quillapp::quillinfoQuillinfo {meta} {
    set project     [project name]
    set description [project description]
    set meta        [list $meta]
} {
    #-------------------------------------------------------------------------
    # TITLE: 
    #    quillinfo.tcl
    #
    # PROJECT:
    #    %project: %description
    #
    # DESCRIPTION:
    #    Project Metadata
    #
    #    Generated by Quill
    #
    #-------------------------------------------------------------------------

    #-------------------------------------------------------------------------
    # Exported Commands

    namespace eval ::quillinfo {
        variable meta
        array set meta %meta

        namespace export \
            project      \
            description  \
            version      \
            homepage

        namespace ensemble create
    }

    #-------------------------------------------------------------------------
    # Commands

    # project
    #
    # Returns the project name.

    proc ::quillinfo::project {} {
        variable meta
        return $meta(project)
    }

    # description 
    #
    # Returns the project's description.

    proc ::quillinfo::description {} {
        variable meta
        return $meta(description)
    }

    # version 
    #
    # Returns the project version number.

    proc ::quillinfo::version {} {
        variable meta
        return $meta(version)
    }

    # homepage 
    #
    # Returns the project home page URL.

    proc ::quillinfo::homepage {} {
        variable meta
        return $meta(homepage)
    }
}