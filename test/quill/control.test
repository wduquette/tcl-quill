# -*-Tcl-*-
#-------------------------------------------------------------------------
# TITLE:
#    control.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    quill(n): Test cases for control.tcl

#-------------------------------------------------------------------------
# Load the tcltest package

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2.3
    eval ::tcltest::configure $argv
}

namespace import ::tcltest::test

#-------------------------------------------------------------------------
# Load the package to be tested

source ../../lib/quill/pkgModules.tcl
namespace import ::quill::*

#-------------------------------------------------------------------------
# Setup

proc echo {args} { return "echo <[join $args ><]>" }

#-------------------------------------------------------------------------
# assert

test assert-1.1 {assert succeeds} -body {
    assert {1 + 1 == 2}
    set result OK
} -result {OK}

test assert-1.2 {assert fails} -body {
    codecatch {
        assert {1 + 1 == 3}
    }
} -result {ASSERT {Assertion failed: 1 + 1 == 3}}

#-----------------------------------------------------------------------
# callwith

test callwith-1.1 {prefix is empty} -body {
    callwith "" a b c
} -result {}

test callwith-2.1 {one word prefix, no args} -body {
    callwith echo
} -result {echo <>}

test callwith-2.2 {one word prefix, 1 arg with whitespace} -body {
    callwith echo "a b c"
} -result {echo <a b c>}

test callwith-2.3 {one word prefix, 2 args} -body {
    callwith echo a b
} -result {echo <a><b>}

test callwith-3.1 {two word prefix, no args} -body {
    callwith [list echo clientArg]
} -result {echo <clientArg>}

test callwith-3.2 {two word prefix, 1 arg with whitespace} -body {
    callwith [list echo clientArg] "a b c"
} -result {echo <clientArg><a b c>}

test callwith-3.3 {two word prefix, 2 args} -body {
    callwith [list echo clientArg] a b
} -result {echo <clientArg><a><b>}

#-------------------------------------------------------------------------
# codecatch

test codecatch-1.1 {script succeeds} -body {
    codecatch {
        set a "No error"
    }
} -result {ok {No error}}

test codecatch-1.2 {script fails} -body {
    codecatch {
        throw FAILURE "Simulated error"
    }
} -result {FAILURE {Simulated error}}

#-------------------------------------------------------------------------
# foroption

test foroption-1.1 {no args} -body {
    set argv ""
    set test 0
    foroption opt argv {
        -test { set test 1 }
    }

    list $test $argv
} -result {0 {}}

test foroption-1.2 {no options} -body {
    set argv {this that}
    set test 0
    foroption opt argv {
        -test { set test 1 }
    }

    list $test $argv
} -result {0 {this that}}

test foroption-1.3 {one option} -body {
    set argv {-test this that}
    set test 0
    foroption opt argv {
        -test { set test 1 }
    }

    list $test $argv
} -result {1 {this that}}

test foroption-1.3 {two options with lshift} -body {
    set argv {-test -num 5 this that}
    set test 0
    set num 0
    foroption opt argv {
        -test { set test 1            }
        -num  { set num [lshift argv] }
    }

    list $test $num $argv
} -result {1 5 {this that}}

test foroption-1.4 {-all, successfully} -body {
    set argv {-test -num 5}
    set test 0
    set num 0
    foroption opt argv -all {
        -test { set test 1            }
        -num  { set num [lshift argv] }
    }

    list $test $num $argv
} -result {1 5 {}}

test foroption-2.1 {unknown option} -body {
    set argv {-test -num 5 this that}
    set test 0
    set num 0

    codecatch {
        foroption opt argv {
            -test { set test 1 }
        }
    }
} -result {INVALID {Unknown option: "-num"}}

test foroption-2.2 {-all, unsuccessfully} -body {
    set argv {-test this that}
    set test 0
    set num 0

    codecatch {
        foroption opt argv -all {
            -test { set test 1 }
        }
    }
} -result {INVALID {Unknown option: "this"}}

#-------------------------------------------------------------------------
# precond

test precond-1.1 {precondition is met} -body {
    codecatch {
        precond {1 + 1 == 2} "Math works"
    }
} -result {ok {}}

test precond-1.2 {script fails} -body {
    codecatch {
        precond {1 + 1 == 3} "Math fails"
    }
} -result {ASSERT {Math fails}}

#-------------------------------------------------------------------------
# require

test require-1.1 {requirement is met} -body {
    codecatch {
        require {1 + 1 == 2} "Math works"
    }
} -result {ok {}}

test require-1.2 {requirement is not met} -body {
    codecatch {
        require {1 + 1 == 3} "Math fails"
    }
} -result {INVALID {Math fails}}

test require-1.3 {can specify error code} -body {
    codecatch {
        require {1 + 1 == 3} "Math fails" SYNTAX
    }
} -result {SYNTAX {Math fails}}

#---------------------------------------------------------------------
# Clean up and finish

::tcltest::cleanupTests



