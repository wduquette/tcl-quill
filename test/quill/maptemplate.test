# -*-Tcl-*-
#-----------------------------------------------------------------------
# TITLE:
#    maptemplate.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    Test cases for maptemplate(n)

#-------------------------------------------------------------------------
# Load the tcltest package

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2.3
    eval ::tcltest::configure $argv
}

namespace import ::tcltest::test

#-------------------------------------------------------------------------
# Load the package to be tested

package require quill
namespace import ::quill::*


#---------------------------------------------------------------------
# maptemplate

test maptemplate-1.1 {constant maptemplate} -body {
    maptemplate const {} {const}
    const
} -result {const}

test maptemplate-1.2 {maptemplate with arguments} -body {
    maptemplate withargs {a b} {---%a---%b---}
    withargs hello world
} -result {---hello---world---}

test maptemplate-1.3 {maptemplate with initbody} -body {
    maptemplate withinit {} {
        set a hello
        set b world
    } {---%a---%b---}
    withinit
} -result {---hello---world---}

test maptemplate-1.4 {templates are defined in caller's namespace} -body {
    namespace eval ::foo:: {
        maptemplate infoo {} {[namespace current]}
    }
    list [info commands infoo] [info commands ::foo::infoo]
} -result {{} ::foo::infoo}


#---------------------------------------------------------------------
# mapsubst

test mapsubst-1.1 {constant mapsubst} -body {
    mapsubst {const}
} -result {const}

test mapsubst-1.2 {variable substitution} -setup {
    proc test_mapsubst-1.2 {} {
        set var Howdy
        mapsubst {---%var---}
    }
} -body {
    test_mapsubst-1.2
} -result {---Howdy---}

test mapsubst-1.3 {outdents} -body {
    mapsubst {
        ---Text---
    }
} -result {---Text---}

#---------------------------------------------------------------------
# Clean up and finish

::tcltest::cleanupTests



