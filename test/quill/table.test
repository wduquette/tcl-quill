# -*-Tcl-*-
#-------------------------------------------------------------------------
# TITLE:
#    table.test
#
# AUTHOR:
#    Will Duquette
#
# DESCRIPTION:
#    quill(n): Test cases for table.tcl

#-------------------------------------------------------------------------
# Load the tcltest package

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest 2.3
    eval ::tcltest::configure $argv
}

namespace import ::tcltest::test

#-------------------------------------------------------------------------
# Load the package to be tested

source ../../lib/quill/pkgModules.tcl
namespace import ::quill::*

#-------------------------------------------------------------------------
# Setup

proc dump {table} {
    set result [join $table "\n    "]
    return "\n    $result\n"
}

#-------------------------------------------------------------------------
# table sort

test sort-1.1 {sort empty table} -body {
    table sort [list]
} -result {}

test sort-1.2 {sort one row} -body {
    dump [table sort {{a 1 b 2 c 3}}]
} -result {
    a 1 b 2 c 3
}

test sort-1.3 {sort on all rows with defaults} -body {
    dump [table sort {
        {a 20 b 4 c 17}
        {a 8  b 5 c 19}
        {a 8  b 3 c 12}
        {a 13 b 5 c 13}
    }]
} -result {
    a 13 b 5 c 13
    a 20 b 4 c 17
    a 8  b 3 c 12
    a 8  b 5 c 19
}

test sort-1.4 {sort on specific columns} -body {
    dump [table sort {
        {a 20 b 4 c 17}
        {a 8  b 5 c 19}
        {a 8  b 3 c 12}
        {a 13 b 5 c 13}
    } {b c}]
} -result {
    a 8  b 3 c 12
    a 20 b 4 c 17
    a 13 b 5 c 13
    a 8  b 5 c 19
}

test sort-1.5 {numeric sort} -body {
    dump [table sort {
        {a 20 b 4 c 17}
        {a 8  b 5 c 19}
        {a 8  b 3 c 12}
        {a 13 b 5 c 13}
    } {{a -numeric} b}]
} -result {
    a 8  b 3 c 12
    a 8  b 5 c 19
    a 13 b 5 c 13
    a 20 b 4 c 17
}

test sort-1.6 {decreasing sort} -body {
    dump [table sort {
        {a 20 b 4 c 17}
        {a 8  b 5 c 19}
        {a 8  b 3 c 12}
        {a 13 b 5 c 13}
    } {{a -numeric} {b -decreasing}}]
} -result {
    a 8  b 5 c 19
    a 8  b 3 c 12
    a 13 b 5 c 13
    a 20 b 4 c 17
}

test sort-1.7 {case sensitive sort} -body {
    dump [table sort {
        {a Alfa     b 4 c 17}
        {a bravo    b 5 c 19}
        {a Charlie  b 3 c 12}
        {a dexter   b 5 c 13}
    }]
} -result {
    a Alfa     b 4 c 17
    a Charlie  b 3 c 12
    a bravo    b 5 c 19
    a dexter   b 5 c 13
}

test sort-1.7 {case insensitive sort} -body {
    dump [table sort {
        {a Alfa     b 4 c 17}
        {a bravo    b 5 c 19}
        {a Charlie  b 3 c 12}
        {a dexter   b 5 c 13}
    } {{a -nocase}}]
} -result {
    a Alfa     b 4 c 17
    a bravo    b 5 c 19
    a Charlie  b 3 c 12
    a dexter   b 5 c 13
}
#---------------------------------------------------------------------
# Clean up and finish

::tcltest::cleanupTests



